# Trollang grammar, based on C, .troll extenstion


# this is like high level blocks
program     := (function)* EOF ;

function    := "fn" IDENT "(" params? ")" block ;
params      := IDENT ("," IDENT)* ;

block       := "{" statement* "}" ;

# control flow statements
statement   := letStmt
             | ifStmt
             | whileStmt
             | returnStmt
             | printStmt
             | exprStmt
             | block ;

letStmt     := "let" IDENT ("=" expr)? ";" ; #IDENT means indent based
ifStmt      := "if" "(" expr ")" statement ("else" statement)? ;
whileStmt   := "while" "(" expr ")" statement ;
returnStmt  := "return" expr? ";" ;
printStmt   := "print" "(" expr ")" ";" ;
exprStmt    := expr ";" ;

# expressions with variables !!
expr        := assignment ;

assignment  := IDENT "=" assignment
             | logic_or ;

logic_or    := logic_and ( "||" logic_and )* ;
logic_and   := equality  ( "&&" equality  )* ;
equality    := compare   ( ("==" | "!=") compare )* ;
compare     := term      ( ("<" | "<=" | ">" | ">=") term )* ;
term        := factor    ( ("+" | "-") factor )* ;
factor      := unary     ( ("*" | "/" | "%") unary )* ;
unary       := ("!" | "-") unary
             | call ;

call        := primary ( "(" args? ")" )* ;
args        := expr ("," expr)* ;

primary     := INT
             | IDENT
             | "(" expr ")" ;
