# Trollang Grammar
# Syntax: Extended BNF with Kleene operators (*, ?, +)

program       := declaration* EOF ;

declaration   := modelDecl
               | funDecl
               | varDecl
               | statement ;

# Declarations
modelDecl     := "model" IDENT "{" funDecl* "}" ;
funDecl       := "fn" IDENT "(" parameters? ")" block ;
varDecl       := "let" IDENT ( "=" expression )? ";" ;
parameters    := IDENT ( "," IDENT )* ;

# Statements
statement     := exprStmt
               | ifStmt
               | whileStmt
               | returnStmt
               | printStmt
               | block ;

block         := "{" declaration* "}" ;
ifStmt        := "if" "(" expression ")" statement ( "else" statement )? ;
whileStmt     := "while" "(" expression ")" statement ;
returnStmt    := "return" expression? ";" ;
printStmt     := "print" "(" expression ")" ";" ;
exprStmt      := expression ";" ;

# Expressions
expression    := assignment ;

assignment    := ( call "=" )* logic_or ;

logic_or      := logic_and ( "||" logic_and )* ;
logic_and     := equality ( "&&" equality )* ;
equality      := comparison ( ( "!=" | "==" ) comparison )* ;
comparison    := term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term          := factor ( ( "-" | "+" ) factor )* ;
factor        := unary ( ( "/" | "*" | "%" | "@" ) unary )* ;

# Unary: ( "!" | "-" )* call
unary         := ( "!" | "-" )* call ;

call          := primary ( "(" arguments? ")" | "." IDENT | "[" expression "]" )* ;
arguments     := expression ( "," expression )* ;

primary       := NUMBER
               | STRING
               | "true" | "false" | "nil"
               | "(" expression ")"
               | IDENT
               | "[" arguments? "]" ;
